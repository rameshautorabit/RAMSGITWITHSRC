trigger Attachment_System on Attachment (before delete, before update) {
	
	/*
	 *	This trigger enforces the rule that in order to edit or delete an existing attachment that
	 *	was created by an automated system process, the user must have Modify All permission. Attachments
	 *	that are generated by an automated system process are identified by a special string in the Description
	 *	field.
	 *
	 */
	
	List<Attachment> attList = new List<Attachment>();
	String systemGenerated = 'System Generated Attachment';
	Boolean modifyAll = false;
	
	
	if (trigger.isUpdate) {
		for (Attachment att :trigger.new) {
			if (String.isNotBlank(trigger.oldMap.get(att.Id).Description) && trigger.oldMap.get(att.Id).Description.startsWith(systemGenerated)) {
				attList.add(att);
			}
		}
	}
	
	if (trigger.isDelete) {
		for (Attachment att :trigger.old) {
			if (String.isNotBlank(att.Description) && att.Description.startsWith(systemGenerated)) {
				attList.add(att);
			}
		}
	}
	
	
	if (!attList.isEmpty()) {
		modifyAll = [select Id, PermissionsModifyAllData from Profile where Id = :UserInfo.getProfileId() limit 1].PermissionsModifyAllData;
		if (!modifyAll) {
			for (Attachment att :attList) {
				att.addError('Insufficient Privileges. You do not have the level of access necessary to perform the operation you requested. Please contact your administrator if access is necessary.');
			}
		}
	}
}